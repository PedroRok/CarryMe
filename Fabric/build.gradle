plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

base {
    archivesName = "${mod_id}-fabric"
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    runs {
        client {
            client()
            configName = "Fabric Client"
            ideConfigGenerated = true
        }

        client2 {
            client()
            configName = "Fabric Client 2"
            ideConfigGenerated = true
            programArgs "--username", "Dev2"
        }

        server {
            server()
            configName = "Fabric Server"
            ideConfigGenerated = true
            runDir = "run"
            programArgs "--nogui"
        }
    }
}

sourceSets {
    main {
        java {
            srcDir project(':Common').file('src/main/java')
        }
        resources {
            srcDir project(':Common').file('src/main/resources')
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    // Include Common as a library
    implementation project(path: ':Common', configuration: 'namedElements')

    // Carry On dependency
    modApi("curse.maven:carry-on-274259:7393887")
}

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(AbstractArchiveTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
    def props = [
        "version": version,
        "minecraft_version": minecraft_version,
        "fabric_loader_version": fabric_loader_version,
        "fabric_api_version": fabric_api_version,
        "mod_id": mod_id,
        "mod_name": mod_name,
        "mod_description": mod_description,
        "mod_authors": mod_authors,
        "mod_license": mod_license
    ]

    inputs.properties(props)
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand props
    }
}

