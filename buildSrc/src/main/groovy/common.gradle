plugins {
    id 'java-library'
    id 'maven-publish'
}

base {
    // carryme-fabric-1.20.1
    archivesName = "${mod_id}-${project.name.toLowerCase()}-${minecraft_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

jar {
    destinationDirectory = rootDir.toPath().resolve("build/libs").toFile()
}

repositories {
    mavenCentral()
    // https://docs.gradle.org/current/userguide/declaring_repositories.html#declaring_content_exclusively_found_in_one_repository
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter { includeGroupAndSubgroups('org.spongepowered') }
    }
    exclusiveContent {
        forRepositories(maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org/'
        },
                maven {
                    name = "NeoForge"
                    url = 'https://maven.neoforged.net/releases'
                })
        filter { includeGroup('org.parchmentmc.data') }
    }
    maven {
        name = 'BlameJared'
        url = 'https://maven.blamejared.com'
    }

    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
    maven {
        url "https://maven.blamejared.com/"
    }
    maven {
        name = "Shedaniel"
        url "https://maven.shedaniel.me/"
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}


['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:${base.archivesName.get()}:$version")
        capability("$group:$mod_id-${project.name}-${minecraft_version}:$version")
        capability("$group:$mod_id:$version")
    }
    publishing.publications.configureEach {
        suppressPomMetadataWarningsFor(variant)
    }
}

sourcesJar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_${mod_name}" }
    }
}

jar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_${mod_name}" }
    }

    manifest {
        attributes(['Specification-Title'   : mod_name,
                    'Specification-Vendor'  : mod_authors,
                    'Specification-Version' : project.jar.archiveVersion,
                    'Implementation-Title'  : project.name,
                    'Implementation-Version': project.jar.archiveVersion,
                    'Implementation-Vendor' : mod_authors,
                    'Built-On-Minecraft'    : minecraft_version])
    }
}

processResources {
    var expandProps = ['mod_version'                  : mod_version,
                       'group'                        : project.group, //Else we target the task's group.
                       'minecraft_version'            : minecraft_version,
                       'minecraft_version_range'      : minecraft_version_range,
                       'fabric_api_version'           : fabric_api_version,
                       'fabric_loader_version'        : fabric_loader_version,
                       'mod_name'                     : mod_name,
                       'mod_authors'                  : mod_authors,
                       'mod_id'                       : mod_id,
                       'mod_license'                  : mod_license,
                       'mod_description'              : project.mod_description,
                       'neoforge_version'             : neoforge_version,
                       'neoforge_loader_version_range': neoforge_loader_version_range,
                       "forge_version"                : forge_version,
                       "forge_loader_version_range"   : forge_loader_version_range,
                       "loader_version_range"         : loader_version_range,
                       'java_version'                 : java_version,
                       carry_on_version               : carry_on_version
    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
}
